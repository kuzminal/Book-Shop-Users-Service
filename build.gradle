plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.kuzmin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
    set('testcontainersVersion', "1.16.0")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'

    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.flywaydb:flyway-core'
    runtimeOnly 'org.springframework:spring-jdbc'

    swaggerCodegen 'org.openapitools:openapi-generator-cli:5.2.0'
    compileOnly 'io.swagger:swagger-annotations:1.6.3'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation:2.5.5'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'io.springfox:springfox-core:3.0.0'
}

test {
    useJUnitPlatform()
}

swaggerSources {
    users {
        inputFile = file("${rootDir}/src/main/resources/api/users.yaml")
        code {
            language = 'spring'
            configFile = file("${rootDir}/src/main/resources/api/config.json")
        }
    }
}

compileJava.dependsOn swaggerSources.users.code
sourceSets.main.java.srcDir "${swaggerSources.users.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.users.code.outputDir}/src/main/resources"

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

bootBuildImage {
    imageName = "kuzmin35/${project.name}:${project.version}"
    environment = ["BP_JVM_VERSION" : "17.*"]
}
