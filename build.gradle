plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.kuzmin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'

    swaggerCodegen 'org.openapitools:openapi-generator-cli:5.2.0'
    compileOnly 'io.swagger:swagger-annotations:1.6.3'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation:2.5.5'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'io.springfox:springfox-core:3.0.0'
}

test {
    useJUnitPlatform()
}

swaggerSources {
    def typeMappings = 'URI=URI, BigDecimal=BigDecimal, EntityModel=EntityModel'
    def importMappings = 'URI=java.net.URI, BigDecimal=java.math.BigDecimal, EntityModel=org.springframework.hateoas.EntityModel'
    users {
        def apiYaml = "${rootDir}/src/main/resources/api/users.yaml"
        def configJson = "${rootDir}/src/main/resources/api/config.json"
        inputFile = file(apiYaml)
        def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
        code {
            language = 'spring'
            configFile = file(configJson)
            rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
                          typeMappings, '--import-mappings', importMappings] as List<String>
            components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
            //templateDir = file("${rootDir}/src/main/resources/templates")
            //dependsOn validation // Should be uncommented once plugin starts supporting OA 3 validation
        }
    }
}

compileJava.dependsOn swaggerSources.users.code
sourceSets.main.java.srcDir "${swaggerSources.users.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.users.code.outputDir}/src/main/resources"
